# pathmunge VAR_NAME /path/to/stuff (after)
pathmunge() {
    local pathlist
    eval "pathlist=\"\$$1\"" 2>/dev/null || return 1
    case ":$pathlist:" in
        *:"$2":*)
            ;;
        "::")
            eval "$1=\"$2\"" 2>/dev/null || return 1
            ;;
        *)
            if [ "$3" = "after" ]; then
                eval "$1=\"$pathlist:$2\"" 2>/dev/null || return 1
            else
                eval "$1=\"$2:$pathlist\"" 2>/dev/null || return 1
            fi
            ;;
    esac
    return 0
}

# attach_or_connect HOST
function attach_or_connect() {
    if [[ $# -lt 1 || $# -gt 2 || $1 == "-*" ]] ; then
        echo 'Usage: attach <host> [socket-name]'
    else
        sock=${2-default}
        ssh -x $1 -t tmux -L ${sock} -f '~/.tmux-iTerm.conf' list-sessions
        if [ "$?" -eq "0" ]; then
            ssh -x $1 -t tmux -L ${sock} -f '~/.tmux-iTerm.conf' -CC attach
        else
            ssh -x $1 -t tmux -L ${sock} -f '~/.tmux-iTerm.conf' -CC
        fi
    fi
}

# Override these from oh-my-zsh to customize them a bit:

# Runs before showing the prompt
function omz_termsupport_precmd {
    emulate -L zsh

    if [[ "$DISABLE_AUTO_TITLE" == true ]]; then
        return
    fi

    # defaults in oh-my-zsh:
    # ZSH_THEME_TERM_TAB_TITLE_IDLE="%15<..<%~%<<" #15 char left truncated PWD
    # ZSH_THEME_TERM_TITLE_IDLE="%n@%m: %~"
    # title $ZSH_THEME_TERM_TAB_TITLE_IDLE $ZSH_THEME_TERM_TITLE_IDLE

    FULL_TITLE="${ZSH_TITLE_PREFIX} %~"

    title $ZSH_THEME_TERM_TAB_TITLE_IDLE $FULL_TITLE
}

# Runs before executing the command
function omz_termsupport_preexec {
    emulate -L zsh
    setopt extended_glob

    if [[ "$DISABLE_AUTO_TITLE" == true ]]; then
        return
    fi

    # cmd name only, or if this is sudo or ssh, the next cmd
    local just_cmd=${1[(wr)^(*=*|sudo|ssh|mosh|rake|-*)]:gs/%/%%}
    local cmd_line=${1:gs/%/%%}
    local full_title=${ZSH_TITLE_PREFIX}${cmd_line}
    title "$just_cmd" "%100>...>$full_title%<<"
}

function +vi-vcs-strip-master() {
    if [[ "$hook_com[branch]" == "master" ]]; then
#       hook_com[branch]='\010'
       hook_com[branch]=' '
    fi
}

# For going to boxes that have CSH as the unchangable default shell
function sz {
    ssh -t $1 "setenv SHELL zsh ; setenv DEFAULT_USERNAMES $DEFAULT_USERNAMES ; zsh -l"
}
