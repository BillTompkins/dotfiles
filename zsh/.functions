# pathmunge VAR_NAME /path/to/stuff (after)
pathmunge() {
    local pathlist
    eval "pathlist=\"\$$1\"" 2>/dev/null || return 1
    case ":$pathlist:" in
        *:"$2":*)
            ;;
        "::")
            eval "$1=\"$2\"" 2>/dev/null || return 1
            ;;
        *)
            if [ "$3" = "after" ]; then
                eval "$1=\"$pathlist:$2\"" 2>/dev/null || return 1
            else
                eval "$1=\"$2:$pathlist\"" 2>/dev/null || return 1
            fi
            ;;
    esac
    return 0
}

# attach_or_connect HOST
function attach_or_connect() {
    if [[ $# -ne 1 ]]; then
        echo 'Usage: attach [host]'
    else
        ssh -x $1 -t tmux -f '~/.tmux-iTerm.conf' -CC attach
        if [ "$?" -eq "1" ]; then
            ssh -x $1 -t tmux -f '~/.tmux-iTerm.conf' -CC
        fi
    fi
}

# Override these from oh-my-zsh to customize them a bit:

# Runs before showing the prompt
function my_omz_termsupport_precmd {
    emulate -L zsh

    if [[ "$DISABLE_AUTO_TITLE" == true ]]; then
        return
    fi

    # defaults in oh-my-zsh:
    # ZSH_THEME_TERM_TAB_TITLE_IDLE="%15<..<%~%<<" #15 char left truncated PWD
    # ZSH_THEME_TERM_TITLE_IDLE="%n@%m: %~"
    # title $ZSH_THEME_TERM_TAB_TITLE_IDLE $ZSH_THEME_TERM_TITLE_IDLE

    if [[ "$DEFAULT_USER" == "$USER" ]]; then
       TITLE_USER=""
       else
           TITLE_USER=${USER}@
    fi
    TITLE_HOST=${HOST:#.local}  # empty if $HOST matches *.local, else $HOST
    FULL_TITLE="${TITLE_USER}${TITLE_HOST}: %~"
    title $ZSH_THEME_TERM_TAB_TITLE_IDLE $FULL_TITLE
}

# Runs before executing the command
function omz_termsupport_preexec {
    emulate -L zsh
    setopt extended_glob

    if [[ "$DISABLE_AUTO_TITLE" == true ]]; then
        return
    fi

    # cmd name only, or if this is sudo or ssh, the next cmd
    local CMD=${1[(wr)^(*=*|sudo|ssh|mosh|rake|-*)]:gs/%/%%}
    local CMD_LINE=${1:gs/%/%%}

    if [[ "$DEFAULT_USER" == "$USER" ]]; then
        TITLE_USER=""
    else
        TITLE_USER=${USER}@
    fi
    TITLE_HOST=${HOST}
    if [[ ${TITLE_HOST} == *.local ]] ; then
        TITLE_HOST=""
    fi
    FULL_TITLE="${TITLE_USER}${TITLE_HOST}"
    if [[ $FULL_TITLE != "" ]] ; then
        FULL_TITLE="${FULL_TITLE}: "
    fi
    FULL_TITLE=${FULL_TITLE}${CMD_LINE}
    title "$CMD" "%100>...>$FULL_TITLE%<<"
}
